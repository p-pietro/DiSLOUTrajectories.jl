# Gauge discovery: shifts ζ_μ^(g) from clusters of preliminary trajectories
# (paper Appendix A.2) or from stable semiclassical fixed points (Appendix A.1).

"""
    discover_gauges(H, c_ops; <keyword arguments>)
    discover_gauges(hamiltonian, collapse_operators; <keyword arguments>)

Discover the paper's shifts `ζ_μ^(g)` (Eq. 9) for [`dislou_solve`](@ref).

The default `method=:trajectories` clusters terminal amplitudes from preliminary
quantum trajectories and requires `Clustering.jl`. Otherwise, you can provide symbolic 
constructors for the Hamiltonian and collapse operators, select `method=:semiclassical` and load `QuantumCumulants.jl` to find
stable mean-field fixed points. Install these optional packages before loading them.

See also [`dislou_solve`](@ref).

# Examples

Find the single vacuum gauge of a damped mode with trajectory discovery:

```jldoctest
julia> using DiSLOUTrajectories, QuantumToolbox, Clustering

julia> a = destroy(2);

julia> gauges = discover_gauges(0.0 * a, [a];
           mode_ops = [a], mode_dims = [2], discovery_time = 1.0,
           seed_radii = [0.0], cluster_scales = [1.0], step = 1.0,
           nseeds = 2, min_neighbors = 1, min_weight = 0.0);

julia> (gauges.method, size(gauges.shifts), all(iszero, gauges.shifts))
(:trajectories, (1, 1), true)
```

Find the same vacuum fixed point from symbolic constructors:

```jldoctest
julia> using DiSLOUTrajectories, QuantumCumulants

julia> # H
       hamiltonian(a) = adjoint(a) * a;

julia> # C = a
       collapse_operators(a) = [a];

julia> gauges = discover_gauges(hamiltonian, collapse_operators;
           method = :semiclassical, limits = (1.0,));

julia> (gauges.method, size(gauges.shifts), all(z -> abs(z) < 1e-9, gauges.shifts))
(:semiclassical, (1, 1), true)
```

# Extended help

Trajectory discovery starts in random coherent product states. Each retained
cluster defines shifts from the negative cluster averages of the physical
collapse-operator expectations. Semiclassical discovery derives first-order bosonic
mean-field equations, finds stable fixed points, and evaluates collapse amplitudes
at those points.

# Arguments

The accepted arguments depend on `method`. The trajectory method is the default;
the semiclassical method must be selected explicitly.

## Trajectory discovery

- `H`: Time-independent Hermitian Hamiltonian as a
  `QuantumToolbox.QuantumObject` operator. Its tensor dimensions must match
  `mode_dims`. This method uses QuantumToolbox's `mcsolve` for the preliminary
  trajectories.
- `c_ops`: Vector of time-independent `QuantumToolbox.QuantumObject` collapse
  operators with the same tensor dimensions as `H`. Include the square root of
  each physical decay rate in the corresponding operator.
- `method::Symbol`: Set to `:trajectories` (the default).
- `mode_ops`: Nonempty collection of bosonic annihilation operators, embedded
  in the full tensor-product Hilbert space. Each operator must have the same
  dimensions as `H`; their order defines the rows of the returned centers.
- `mode_dims`: One integer Hilbert-space truncation dimension, at least `2`,
  per mode, in tensor-product order.
- `discovery_time`: Positive, finite duration of each preliminary trajectory,
  which starts at time zero.
- `seed_radii`: One finite, nonnegative coherent-amplitude radius per mode.
  Initial amplitudes are sampled uniformly in each complex disk.
- `cluster_scales`: Vector of positive, finite amplitude scales, one per mode.
  Real and imaginary parts of each terminal mode mean are divided by its
  scale before Euclidean DBSCAN clustering.
- `step`: Positive, finite spacing of the preliminary observation grid.
  Defaults to `discovery_time / 40`. The endpoint `discovery_time` is always
  included.
- `nseeds::Int`: Positive number of initial coherent product states, with one
  trajectory per state. Defaults to `600`.
- `terminal_window`: Finite, nonnegative duration at the end of the run over
  which sampled mode, occupation, and collapse expectations are averaged.
  Defaults to `0.0`, which uses the final sample. Values greater than
  `discovery_time` use the whole observation grid.
- `preliminary_shifts`: Optional vector of finite complex shifts, one per
  collapse channel, applied during the preliminary runs with a compensating
  Hamiltonian shift. Defaults to `nothing`, meaning zero shifts. Returned
  gauges use expectations of the original physical collapse operators.
- `dbscan_radius`: Positive, finite DBSCAN neighborhood radius in the scaled
  real-imaginary feature space. Defaults to `1.5`.
- `min_neighbors::Int`: Positive DBSCAN core-point neighbor-count threshold,
  passed to `Clustering.dbscan`. Defaults to `10`.
- `min_weight`: Minimum retained cluster population as a fraction of all
  `nseeds` samples. Must be finite and in `[0, 1)`; defaults to `0.02`.
- `rng::AbstractRNG`: Random number generator for the initial amplitudes and
  the preliminary trajectory streams. Defaults to `Random.default_rng()`.
- `save_preliminary_trajectories::Int`: Nonnegative number of preliminary
  trajectories whose state, expectation, and jump traces are retained in the
  diagnostics. Defaults to `0`; values above `nseeds` retain all trajectories.
- `ensemblealg`: How the preliminary trajectories run: `EnsembleThreads()` (default),
  `EnsembleSerial()`, or `EnsembleDistributed()`. Distributed execution requires
  worker processes with DiSLOUTrajectories and QuantumToolbox loaded.

## Semiclassical discovery

- `hamiltonian::Function`: Function `hamiltonian(a₁, a₂, ...)` returning a
  QuantumCumulants Hamiltonian from symbolic bosonic annihilation operators.
- `collapse_operators::Function`: Function `collapse_operators(a₁, a₂, ...)` returning
  a tuple or vector of QuantumCumulants collapse operators. It is also called
  with complex fixed-point amplitudes and must then produce a collection of
  the same length that evaluates to finite physical collapse amplitudes.
  Include square roots of decay rates in both evaluations.
- `method::Symbol`: Set to `:semiclassical`.
- `limits`: Nonempty tuple or vector of positive, finite occupation bounds,
  one per mode. Accepted fixed points satisfy `abs2(α[m]) ≤ limits[m]` up to
  numerical tolerance. These bounds define the search region.
- `parameters`: Tuple, vector, or dictionary of unique symbolic
  `parameter => value` pairs with finite numeric values. The keys must exactly
  match the parameters of the compiled mean-field system. Defaults to `()`
  for a model without symbolic parameters.

# Notes

- Load `Clustering.jl` together with `DiSLOUTrajectories.jl` before calling
  `method=:trajectories`. It activates the extension that implements the
  DBSCAN clustering step.
- Load `QuantumCumulants.jl` together with `DiSLOUTrajectories.jl` before calling
  `method=:semiclassical`.
- The two methods have separate keyword sets. Arbitrary `mcsolve` or ODE
  solver options are not forwarded.
- Trajectory cluster weights are fractions of all preliminary samples,
  including discarded samples in the denominator. Their sum can be less than
  one. Clusters are ordered by decreasing weight, then by their centers.
  Noise points and samples from discarded clusters receive label `0`.
- Stability of the semiclassical solution is determined
  from the real mean-field Jacobian. A finite multistart search can miss roots,
  and its cost grows rapidly with the number of modes.
- Discovery raises an `ArgumentError` if no trajectory cluster or stable
  semiclassical fixed point is retained.

# Returns

- `gauges::NamedTuple`: `(; shifts, method, centers, weights, diagnostics)`.
  With `Nc` collapse channels, `Nm` modes, and `Ng` retained gauges:
  - `shifts::Matrix{ComplexF64}`: `Nc × Ng` shifts `ζ_μ^(g)`; column `g` defines
    `C_μ^(g) = C_μ + ζ_μ^(g) I` (Eq. 9) for each channel `μ`.
  - `method::Symbol`: `:trajectories` or `:semiclassical`.
  - `centers::Matrix{ComplexF64}`: `Nm × Ng` cluster-mean mode amplitudes
    or stable fixed-point amplitudes.
  - `weights::Vector{Float64}`: `Ng` cluster population fractions or uniform
    semiclassical weights, as described above.
  - `diagnostics::NamedTuple`: Method-specific discovery data described below.

For trajectory discovery, `diagnostics` contains:

- `counts` and `labels`: Cluster sizes of length `Ng` and assignments of
  length `nseeds`. Positive labels index columns of `centers`.
- `terminal_means` and `terminal_occupations`: `Nm × nseeds` terminal-window
  averages of mode amplitudes and occupations.
- `terminal_collapse_means`: `Nc × nseeds` terminal-window averages of the
  physical collapse-operator expectations.
- `times`: Preliminary observation grid, including zero and `discovery_time`.
- `preliminary_traces`: `(; indices, states, means, occupations)` for the
  retained preliminary runs. `indices` contains their one-based seed indices;
  the other fields are vectors of matrices. For `Nt = length(times)` and
  `N = prod(mode_dims)`, each state matrix is `N × Nt`, and each mode-mean
  or occupation matrix is `Nm × Nt`. These vectors are empty by default.
- `preliminary_jump_times` and `preliminary_jump_channels`: Vectors
  of event-time and one-based collapse-channel vectors for the retained runs.
- The resolved discovery settings: `mode_dims`, `discovery_time`, `seed_radii`,
  `cluster_scales`, `step`, `nseeds`, `terminal_window`, `preliminary_shifts`,
  `dbscan_radius`, `min_neighbors`, `min_weight`, and
  `save_preliminary_trajectories`. The last field records the retained count,
  capped at `nseeds`.

For semiclassical discovery, `diagnostics` contains:

- `roots`: Matrix with one column per distinct fixed point found, including
  unstable points, and one row per mode.
- `stability`: One `(; stable, max_real_eigenvalue, residual)` record per
  column of `roots`. `max_real_eigenvalue` is the largest real part of the
  Jacobian eigenvalues, and `residual` is the norm of the mean-field drift.
- `rejected`: Matrix of the unstable fixed points excluded from `centers`.
- `stable`: Boolean vector of length `Ng`, with every entry `true`.
"""
discover_gauges(model, collapse_operators; method::Symbol = :trajectories, kwargs...) =
    _discover_gauges(Val(method), model, collapse_operators; kwargs...)

# Methods live in DiSLOUTrajectoriesClusteringExt (Val{:trajectories}) and
# DiSLOUTrajectoriesQuantumCumulantsExt (Val{:semiclassical}).
function _discover_gauges end

const _DISCOVERY_EXTENSIONS = (
    trajectories = (:DiSLOUTrajectoriesClusteringExt, "Clustering"),
    semiclassical = (:DiSLOUTrajectoriesQuantumCumulantsExt, "QuantumCumulants"),
)

# Turns the bare MethodError of an unloaded or unknown method into an actionable hint.
function _discovery_error_hint(io, exc, argtypes, kwargs)
    f = exc.f === Core.kwcall && length(exc.args) >= 2 ? exc.args[2] : exc.f
    f === _discover_gauges && !isempty(argtypes) && argtypes[1] <: Val || return
    method = argtypes[1].parameters[1]
    if !haskey(_DISCOVERY_EXTENSIONS, method)
        print(io, "\nUnsupported gauge discovery method $(repr(method)); use :trajectories or :semiclassical.")
        return
    end
    extension, package = _DISCOVERY_EXTENSIONS[method]
    Base.get_extension(@__MODULE__, extension) === nothing || return
    print(io, "\nmethod=:$method requires $package; run `using $package` before calling discover_gauges.")
    return
end

# Paper Eqs. (A.7–A.8): one trajectory from each random coherent state, averaging
# the mode amplitudes and the collapse expectations over the terminal window.
# Used by the Clustering extension; it lives here so that distributed workers can
# run it without loading Clustering.
function _run_preliminary_trajectories(
        H, c_ops, shifts, mode_ops, mode_dims, discovery_time, seed_radii, step, nseeds,
        terminal_window, rng, save_preliminary_trajectories, ensemblealg
    )
    tlist = collect(0.0:float(step):float(discovery_time))
    last(tlist) < discovery_time && push!(tlist, float(discovery_time))
    tail = findall(>=(discovery_time - terminal_window), tlist)
    isempty(tail) && (tail = [lastindex(tlist)])

    Hrun, Crun = all(iszero, shifts) ? (H, c_ops) : _shifted_operators(H, c_ops, shifts)
    nmodes = length(mode_ops)
    e_ops = vcat(collect(mode_ops), [op' * op for op in mode_ops], collect(c_ops))
    modes, occupations, collapses = 1:nmodes, nmodes .+ (1:nmodes), 2nmodes .+ eachindex(c_ops)

    # Initial amplitudes uniform in a disk of radius `seed_radii[mode]`, and one seed per run.
    amplitudes = [seed_radii[mode] * sqrt(rand(rng)) * cis(2π * rand(rng)) for mode in 1:nmodes, _ in 1:nseeds]
    seeds = [rand(rng, UInt64) for _ in 1:nseeds]
    nsave = min(save_preliminary_trajectories, nseeds)

    function relax(point)
        ψ0 = tensor((coherent(Int(mode_dims[mode]), amplitudes[mode, point]) for mode in 1:nmodes)...)
        sol = mcsolve(
            Hrun, ψ0, tlist, Crun; e_ops, ntraj = 1, rng = Xoshiro(seeds[point]), saveat = tlist,
            keep_runs_results = Val(true), ensemblealg = EnsembleSerial(), progress_bar = Val(false)
        )
        expect = sol.expect[:, 1, :]
        terminal = vec(sum(expect[:, tail]; dims = 2)) / length(tail)
        point <= nsave || return (; terminal, trace = nothing)
        states = stack(ψ.data for ψ in vec(sol.states))
        return (; terminal, trace = (; states, expect, col_times = sol.col_times[1], col_which = sol.col_which[1]))
    end

    results = _map_seeds(relax, nseeds, ensemblealg)
    averages = stack(result.terminal for result in results)
    traces = [results[point].trace for point in 1:nsave]
    return (;
        tlist, nsave,
        terminal_means = averages[modes, :],
        terminal_occupations = real.(averages[occupations, :]),
        terminal_collapse_means = averages[collapses, :],
        states = [trace.states for trace in traces],
        traces = [trace.expect[modes, :] for trace in traces],
        occupations = [real.(trace.expect[occupations, :]) for trace in traces],
        jump_times = [trace.col_times for trace in traces],
        jump_channels = [trace.col_which for trace in traces],
    )
end

# Run `f(i)` for `i in 1:n` as requested by `ensemblealg`.
_map_seeds(f, n, ::EnsembleSerial) = map(f, 1:n)
_map_seeds(f, n, ::EnsembleThreads) = fetch.([Threads.@spawn f(i) for i in 1:n])
_map_seeds(f, n, ::EnsembleDistributed) = Distributed.pmap(f, 1:n)
